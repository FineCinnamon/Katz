/*
 * Copyright (C) 2017 The Kategory Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: 'detekt.gradle'

buildscript {
    ext {
        // Constants
        gradleVersion = '3.5'
        gradleVersionsPluginVersion = '0.15.0'
        javaVersion = '1.7'
        kategoryVersion = '0.1-SNAPSHOT'
        kotlinTestVersion = '2.0.3'
        kotlinVersion = '1.1.4-dev-600'
        kotlinxCollectionsImmutableVersion = '0.1'
        kotlinxCoroutinesVersion = '0.16'
    }

    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    group = 'com.github.finecinnamon'
    archivesBaseName = project.name
    version = kategoryVersion

    jacoco {
        toolVersion '0.7.8'
    }

    task codeCoverageReport(type: JacocoReport) {
        reports {
            xml.enabled true
            xml.destination "${buildDir}/reports/jacoco/report.xml"
            html.enabled true
            csv.enabled false
        }

        classDirectories = fileTree(
                dir: 'build/classes/main/kategory',
        )

        sourceDirectories = files('src/main/kotlin')
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    }

}

task wrapper(type: Wrapper) {
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

apply plugin: 'com.github.ben-manes.versions'
